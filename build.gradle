plugins {
    id 'maven-publish'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.2'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.base', 'javafx.graphics', 'javafx.media', 'javafx.swing' ]
}

repositories {
    mavenCentral()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    maven {
        url 'https://maven.pkg.github.com/juhu1705/configmanager'
        credentials {
            username = System.getenv('USERNAME')
            password = System.getenv('TOKEN')
        }
    }

    maven {
        url 'https://maven.pkg.github.com/juhu1705/logger'
        credentials {
            username = System.getenv('USERNAME')
            password = System.getenv('TOKEN')
        }
    }

    maven {
        url 'https://maven.pkg.github.com/juhu1705/tablefilemanager'
        credentials {
            username = System.getenv('USERNAME')
            password = System.getenv('TOKEN')
        }
    }


    maven {
        url 'https://maven.pkg.github.com/juhu1705/eventmanager'
        credentials {
            username = System.getenv('USERNAME')
            password = System.getenv('TOKEN')
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    implementation('org.kordamp.ikonli:ikonli-javafx:12.2.0') {
        exclude group: 'org.openjfx'
    }
    implementation('org.kordamp.ikonli:ikonli-fontawesome5-pack:12.2.0') {
        exclude group: 'org.openjfx'
    }
    implementation('org.kordamp.ikonli:ikonli-fontawesome-pack:12.2.0') {
        exclude group: 'org.openjfx'
    }
    implementation('org.jfxtras:jmetro:11.6.15') {
        exclude group: 'org.openjfx'
    }
    implementation('org.controlsfx:controlsfx:11.1.0') {
        exclude group: 'org.openjfx'
    }

    implementation 'de.noisruker:logger:1.0.2'
    implementation('de.noisruker:table-filemanager:1.0.0')  {
        exclude group: 'org.openjfx'
        exclude group: 'xml-apis'
        exclude group: 'org.apache.xmlgraphics'
    }
    implementation('de.noisruker:configmanager:1.0.2') {
        exclude group: 'org.openjfx'
    }
    implementation 'de.noisruker:event-manager:1.0.2'
    implementation('org.apache.xmlgraphics:batik-all:1.14') {
        exclude group: 'org.apache.xmlgraphics'
    }
    implementation('org.apache.xmlgraphics:xmlgraphics-commons:2.6') {
        exclude group: 'commons-logging'
    }
    implementation 'org.codehaus.woodstox:stax2-api:4.2.1'
    implementation 'com.fasterxml.woodstox:woodstox-core:6.2.6'
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
java { modularity.inferModulePath = true }
modularity.disableEffectiveArgumentsAdjustment()

group = 'de.juhu'
version = '2.1.0'
description = 'Ein Programm zum Zuweisen von SchÃ¼ler in ihre Wunschkurse'
sourceCompatibility = 16

ext {
    moduleName = 'de.juhu.carp'
    moduleLaunchPoint = 'de.juhu.guiFX.GUILoader'
}

application {
    mainModule = 'de.juhu.carp'
    mainClass = 'de.juhu.guiFX.GUILoader'
}

jlink {
    options = ['--strip-debug', '--ignore-signing-information', '--compress', '2', '--no-header-files', '--no-man-pages']
    mergedModuleJarVersion = '16'

    //targetPlatform('linux-x64', 'G:\\Programme\\Java\\jdk-16.0.2')

    mergedModule {
        // additive = true  // redundant, because excludeXXX() methods are also present

        excludeProvides servicePattern: 'org.codehaus.stax2.validation.XMLValidationSchemaFactory.*'
        excludeProvides servicePattern: 'org.apache.xalan.extensions.bsf.*'
        excludeProvides service: 'org.codehaus.stax2.validation.XMLValidationSchemaFactory.dtd'
    }
    launcher {
        name = rootProject.name
        jvmArgs = ['-Dlog4j.configurationFile=./log4j2.xml']
        noConsole = true
    }
    addExtraDependencies("javafx")

    jpackage {
        installerOptions += [
                '--description', project.description,
                '--version', '2.0.0',
                '--copyright', 'Copyrigth 2021 Fabius Mettner',
                '--license-file', 'G://Projects/JavaProjects/CaRP/LICENSE.txt'
        ]
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageName = rootProject.name
            icon = 'src/main/resources/assets/textures/logo/CaRP.ico'
            installerOptions = ['--win-dir-chooser',
                                '--win-menu', '--win-menu-group', '']
        }
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            icon = 'src/main/resources/assets/textures/logo/CaRP.png'
            installerOptions += [
                    '--linux-deb-maintainer', 'fabius.mettner@gmx.de',
                    '--linux-menu-group', 'Office',
                    '--linux-rpm-license-type', 'Apachev2.0',
                    '--linux-app-category', 'Office',
                    '--linux-shortcut'
            ]
        }
    }
}

tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/image/bin")
    }
}

jar {
    manifest {
        attributes('Main-Class': 'de.juhu.carp/de.juhu.guiFX.GUILoader')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        File f = new File("$buildDir/resources/main/version.properties")

        if(f.exists()) {
            f.withWriter { w ->
                Properties p = new Properties()
                p['version'] = project.version.toString()
                p['name'] = rootProject.name.toString()
                p.store w, null
            }
            // copy needed, otherwise the bean VersionController can't load the file at startup when running complete-app tests.
            copy {
                from "$buildDir/resources/main/version.properties"
                into "bin/main/"
            }
        }
    }
}
classes {
    dependsOn createProperties
}

mainClassName = "${moduleLaunchPoint}"